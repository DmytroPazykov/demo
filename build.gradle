buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.qameta.allure:allure-gradle:2.5"
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.19"
    }
}

plugins {
    id 'java'
    id 'checkstyle'
    id 'idea'
    id 'pmd'

    id "io.qameta.allure" version "2.5"
    id "net.ltgt.apt" version "0.19"
}


idea {
    project {
        languageLevel = "1.11"
    }
}

group 'demo'
version '0.0.0'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

ext {
    junit = '5.3.2'
    junitLauncher = '1.3.2'
    sl4jSimple = '1.7.25'
    lombok = '1.18.4'
    apacheCollections = '4.2'
    apacheCommons = '3.8.1'
}

dependencies {
    implementation "org.junit.jupiter:junit-jupiter-api:$junit"
    implementation "org.junit.platform:junit-platform-launcher:$junitLauncher"
    implementation "org.junit.jupiter:junit-jupiter-engine:$junit"
    implementation "org.junit.vintage:junit-vintage-engine:$junit"
    implementation "org.slf4j:slf4j-simple:$sl4jSimple"
    implementation "org.projectlombok:lombok:$lombok"
    implementation "org.apache.commons:commons-collections4:$apacheCollections"
    implementation "org.apache.commons:commons-lang3:$apacheCommons"
    
    annotationProcessor "org.projectlombok:lombok:$lombok"

}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }

    test.testLogging.showStandardStreams = true
    test.outputs.upToDateWhen {false}


    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

allure {
    autoconfigure = true
    version = '2.7.0'

    useJUnit5 {
        version = '2.8.1'
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(Pmd) {
    reports {
        xml.enabled false
        html.enabled true
    }
}